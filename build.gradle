plugins {
    id "nebula.ospackage" version "8.5.4"
    id "com.jfrog.bintray" version "1.8.5"
    id "de.undercouch.download" version "4.1.1"
}

allprojects {
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        publish = true
        override = true

        pkg {
            userOrg = 'chaos-systems'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/chaos-systems/packages'
            repo = 'deb'

            filesSpec {
                from('build/distributions') {
                    include '**/*.deb'
                }
                into '.'
            }

            debian {
                distribution = 'stable'
                component = 'main'
                architecture = 'all,amd64,arm64,armhf,i386,riscv64'
            }

            version {
                gpg {
                    sign = true
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'nebula.ospackage'
    apply plugin: 'de.undercouch.download'

    build.dependsOn buildDeb
    build.dependsOn buildRpm
}

bintrayUpload.enabled = false
