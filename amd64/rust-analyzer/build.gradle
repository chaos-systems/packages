ospackage {
    packageName = 'rust-analyzer'
    version = "${rustanalyzerVersion}"
    release = '2'
    os = LINUX
}

task downloadZipFile(type: Download) {
    src([
            "https://github.com/rust-lang/rust-analyzer/releases/download/${rustanalyzerVersion}/rust-analyzer-x86_64-unknown-linux-gnu.gz"
    ])
    dest "$buildDir/rust-analyzer.gz"
    overwrite false
}

import java.util.zip.GZIPInputStream
task downloadAndUnzipFile(dependsOn: downloadZipFile) {
    doLast {
        def input = new GZIPInputStream(new FileInputStream("${buildDir}/rust-analyzer.gz"))
        def output = new FileOutputStream("${buildDir}/rust-analyzer")
        def buffer = new byte[1024]
        int length
        while ((length = input.read(buffer)) > 0) {
            output.write(buffer, 0, length)
        }
        output.close()
        input.close()
    }
}

buildRpm {
    arch = "x86_64"
    from(buildDir) {
        into '/usr/local/bin'
        include "rust-analyzer"
        fileMode 0755
    }
}

buildDeb {
    arch = "amd64"
    from(buildDir) {
        into '/usr/local/bin'
        include "rust-analyzer"
        fileMode 0755
    }
}

buildRpm.dependsOn downloadAndUnzipFile
buildDeb.dependsOn downloadAndUnzipFile
